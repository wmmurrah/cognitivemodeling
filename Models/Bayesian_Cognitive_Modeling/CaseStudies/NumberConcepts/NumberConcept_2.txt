# Knower Level Model Applied to Fast-Cards Data
model {
  # Data
  for (i in 1:ns){
    for (j in 1:fnq[i]){  
      # Probability a z[i]-Knower Will Give fa[i,j] when Asked for fq[i,j]
      fa[i,j] ~ dcat(fnpiprime[z[i],fq[i,j],1:fn])
    }
    # Posterior Predictive
    for (j in 1:gn){
      predfa[i,j] ~ dcat(fnpiprime[z[i],j,1:fn])
    }
  }
  # Model
  for (i in 1:nz){
    for (j in 1:gn){
      for (k in 1:fn){
        fpiprimetmp[i,j,k,1] <- fpi[k]
        fpiprimetmp[i,j,k,2] <- 1/v*fpi[k]
        fpiprimetmp[i,j,k,3] <- v*fpi[k]
        # Will be 1 if Knower-Level (i.e, i-1) is Same or Greater than Answer
        find1[i,j,k] <- step((i-1)-k)
        # Will be 1 for the Possible Answer that Matches the Question
        find2[i,j,k] <- equals(k,j)
        # Will be 1 for 0-Knowers
        find3[i,j,k] <- equals(i,1)
        # Will be 1 for HN-Knowers
        find4[i,j,k] <- equals(i,nz)
        find5[i,j,k] <- find3[i,j,k]+find4[i,j,k]*(2+find2[i,j,k])
                        + (1-find4[i,j,k])*(1-find3[i,j,k])
                        * (find1[i,j,k]+find1[i,j,k]*find2[i,j,k]+1)
        fpiprime[i,j,k] <- fpiprimetmp[i,j,k,find5[i,j,k]]
        fnpiprime[i,j,k] <- fpiprime[i,j,k]/sum(fpiprime[i,j,1:fn])
      } 
    }
  }
  # Posterior Prediction For Knower Levels
  for (i in 1:nz){
    for (j in 1:gn){
      predz[i,j] ~ dcat(fnpiprime[i,j,1:fn])
    }
  }      
  # Base rate
  for (i in 1:fn){
    fpitmp[i] ~ dunif(0,1)
    fpi[i] <- fpitmp[i]/sum(fpitmp[1:fn])
  }
  predfpi ~ dcat(fpi[1:fn]) 
  # Priors   
  v ~ dunif(1,1000)
  for (i in 1:ns) {
    z[i] ~ dcat(priorz[])
  }
  for (i in 1:nz){
    priorz[i] <- 1/6
  }      
}  