# Stop
model{
  # Data
  for (i in 1:ns){
    for (q in 1:nq){
      y[i,q] ~ dbern(dec[t[i,q,z[i]]])
      ypred[i,q] ~ dbern(dec[t[i,q,z[i]]])
    }
  }	  
  # TTB Decision
  for (i in 1:ns){
    for (q in 1:nq){	
      # Add Cue Contributions To Mimic TTB Decision
      for (j in 1:nc){
        tmp1[i,q,j] <- (m[p[q,1],j]-m[p[q,2],j])*pow(2,s[j]-1)
      }
      # Find if Cue Favors First, Second, or Neither Stimulus
      tmp2[i,q] <- sum(tmp1[i,q,1:nc])
      tmp3[i,q] <- -1*step(-tmp2[i,q])+step(tmp2[i,q])
      t[i,q,1] <- tmp3[i,q]+2
    }
  }
  # WADD Decision
  for (i in 1:ns){
    for (q in 1:nq){
      for (j in 1:nc){
        tmp4[i,q,j] <- (m[p[q,1],j]-m[p[q,2],j])*x[j]
      }
      # Find if Cue Favors First, Second, or Neither Stimulus
      tmp5[i,q] <- sum(tmp4[i,q,1:nc])
      tmp6[i,q] <- -1*step(-tmp5[i,q])+step(tmp5[i,q])
      t[i,q,2] <- tmp6[i,q]+2
    }
  }
  # Follow Decision With Probability Gamma, or Guess
  dec[1] <- 1-gamma
  dec[2] <- 0.5
  dec[3] <- gamma
  # Cue Search Order Follows Validities
  for (j in 1:nc){
    stmp[j] <- nc-j+1
    s[j] <- rank(v[1:nc],stmp[j])
  }
  # TTB and WADD Subjects in Latent Mixture
  for (i in 1:ns){
    z[i] ~ dcat(base[1:2])
  }
  base[1] <- 0.5
  base[2] <- 0.5
  # Priors
  gamma ~ dunif(0.5,1)
  phi ~ dbeta(1,1)
}